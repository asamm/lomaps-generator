apply plugin: 'java-library'
apply plugin: 'maven-publish'

dependencies {
    implementation project(":mapsforge-core")
    implementation 'com.google.guava:guava:28.1-jre'
    implementation 'net.sf.trove4j:trove4j:3.0.3'
    implementation 'org.locationtech.jts:jts-core:1.16.1'
    compileOnly 'org.openstreetmap.osmosis:osmosis-core:0.47'
    testImplementation "junit:junit:$jUnitVersion"
}

sourceSets.test.compileClasspath += configurations.compileClasspath - configurations.runtimeClasspath
sourceSets.test.runtimeClasspath += configurations.compileClasspath - configurations.runtimeClasspath

def version = '0.10.0'

jar {
    from { configurations.compileClasspath  .collect { it.isDirectory() ? it : zipTree(it) } }
    duplicatesStrategy DuplicatesStrategy.INCLUDE
    from('src/main/config/') {
        filter {
            line -> line.replace('${mapfile.writer.version}', version)
        }
    }
}

task fatJar(type: Jar, dependsOn: configurations.runtimeClasspath) {
    classifier 'jar-with-dependencies'
    duplicatesStrategy DuplicatesStrategy.INCLUDE
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }

    }
    with jar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact fatJar
        }
    }
}

if (project.hasProperty("SONATYPE_USERNAME")) {
    afterEvaluate {
        project.apply from: "${rootProject.projectDir}/deploy.gradle"
    }
}

def releaseTask = tasks.create(name: '_releaseMapsForgeMapWriter') {
    doLast {
        println("Copy " + tasks.getAt("jar").archivePath)
        copy {
            from tasks.getAt("jar").archivePath
            into "../_work/osmosis/plugins"
        }
    }

}
releaseTask.dependsOn jar

