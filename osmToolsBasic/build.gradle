plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.lombok' version '2.0.10'
    id 'io.freefair.lombok' version '8.14.2'
    id 'org.jetbrains.kotlin.plugin.serialization'
}
configurations { providedCompile }

repositories {
    // Asamm private repository
    maven {
        url = uri("https://maven.pkg.github.com/asamm/locus-api-store")
        credentials {
            username = System.getenv("GPR_USERNAME")
            password = System.getenv("GPR_TOKEN")
        }
    }

    // Maven repo
    maven {
        url  "https://jcenter.bintray.com"
    }
    mavenCentral()
}

dependencies {
    implementation(project(":osmToolsCore"))
    implementation(project(":osmToolsDataWriter"))
    implementation(project(":maptiler-api"))

    implementation("com.github.ajalt.clikt:clikt:4.4.0")
    implementation("com.asamm.locus:api-store:1.2.26")
    implementation("org.apache.httpcomponents.client5:httpclient5:5.5")
    implementation("org.apache.httpcomponents:httpcore:4.3.2")
    implementation("net.minidev:json-smart:2.5.2")
    implementation("javax.mail:mail:1.4.1")
    implementation("org.openstreetmap.osmosis:osmosis-core:0.49.2")
    implementation("commons-io:commons-io:2.14.0")
    implementation("net.sf.kxml:kxml2:2.2.2")
    implementation("org.locationtech.jts:jts-core:1.20.0")
    implementation("org.apache.commons:commons-lang3:3.18.0")
    implementation("org.xerial:sqlite-jdbc:3.41.2.2")

    compileOnly("org.projectlombok:lombok:1.18.32")
    annotationProcessor("org.projectlombok:lombok:1.18.32")

    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.1")
}

def versionCode = "0.7.8"
def newArchiveName = "OsmToolsBasic_${versionCode}.jar"

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Main-Class': 'com.asamm.osmTools.OsmToolsCommandKt'
    }

    archiveFileName = newArchiveName;
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from('src/main/config/') {
        include 'plugin.xml'
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "21"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "21"
    }
}


tasks.register('createPlanetUpdateShScript') {
    dependsOn jar
    doLast {
        def jarFile = new File(buildDir, "libs/${newArchiveName}")
        def scriptFile = new File(buildDir, "libs/update_planet_file.sh")
        scriptFile.text = """#!/bin/bash
        # Export paths to find pyosmium-up-to-date script
        export PATH=\$PATH:/home/osmtools/.local/bin
        # Get the absolute path of the directory where the script is located
        SCRIPT_DIR="\$(cd "\$(dirname "\${BASH_SOURCE[0]}")" && pwd)"
        # Change the current working directory to the script's directory
        cd "\$SCRIPT_DIR" || exit 1
        java -jar "\$SCRIPT_DIR/${jarFile.name}" update_planet
        """.stripIndent()
        scriptFile.setExecutable(true)
    }
}

tasks.register('buildWithPlanetUpdateShScript') {
    dependsOn jar, createPlanetUpdateShScript
}